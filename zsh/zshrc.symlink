echo ">>> .zshrc"

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  echo "  source path file: $file"
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  echo "  source other: $file"
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
echo "  autoload -U compinit"
autoload -U compinit
echo "  compinit"
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  echo "  source completion: $file"
  source $file
done

unset config_files

echo "<<< .zshrc"
